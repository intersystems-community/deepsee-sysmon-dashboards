<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="kutac.monitor.utils.Installer">
<TimeCreated>63732,50627.865496</TimeCreated>

<XData name="Install">
<XMLNamespace>INSTALLER</XMLNamespace>
<Data><![CDATA[
<Manifest>
<IfNotDef Var="Namespace">
<Var Name="Namespace" Value="SYSMON"/>
<Log Text="Set namespace to ${Namespace}" Level="0"/>
</IfNotDef>


<If Condition='(##class(Config.Namespaces).Exists("${Namespace}")=0)'>
<Log Text="Creating namespace ${Namespace}" Level="0"/>

<Namespace Name="${Namespace}" Create="yes" Code="${Namespace}" Ensemble="" Data="${Namespace}">
<Log Text="Creating database ${Namespace}" Level="0"/>

<Configuration>
<Database Name="${Namespace}" Dir="${MGRDIR}/${Namespace}" Create="yes" MountRequired="true" Resource="%DB_${Namespace}" PublicPermissions="RW" MountAtStartup="true"/>
</Configuration>

<Log Text="End creating database ${Namespace}" Level="0"/>
</Namespace>

 <Log Text="End creating namespace ${Namespace}" Level="0"/>
</If>


<If Condition='(##class(Security.Applications).Exists($ZCONVERT("/csp/${Namespace}", "L"))=0)'>
<Namespace Name="${Namespace}" Create="no">
<Log Text="Creating web application /csp/${Namespace}" Level="0"/>

<CSPApplication Url='#{$ZCONVERT("/csp/${Namespace}", "L")}' Directory='#{##class(%DeepSee.Report.UI.reportModelServer).GetCSPDirectory()_$ZCONVERT("${Namespace}", "L")}' AuthenticationMethods="64" IsNamespaceDefault="true" Recurse="true" Grant="%All"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="EnableDeepSee"/>

<Log Text="End creating web application /csp/${Namespace}" Level="0"/>
</Namespace>
</If>

<Namespace Name="${Namespace}" Create="no" Code="${Namespace}" Data="${Namespace}" Ensemble="">
<Log Text="Mapping SYS.History package to choosed namespace." Level="0"/>

<Configuration>
<ClassMapping From="CACHESYS" Package="SYS.History"/>
</Configuration>

<Log Text="Mapping SYS.History global to choosed namespace." Level="0"/>

<Configuration>
<GlobalMapping From="CACHESYS" Global="SYS.History"/>
</Configuration>

<Log Text="Mapping MONEXT routine to choosed namespace." Level="0"/>

<Configuration>
<RoutineMapping From="CACHESYS" Routines="MONEXT"/>
</Configuration>
</Namespace>

<Namespace Name="${Namespace}" Create="no">
<IfDef Var="SourceDir">
<Log Text="SourceDir defined - offline install from ${SourceDir}" Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="OfflineInstall"/>
</IfDef>

<IfNotDef Var="SourceDir">
<Log Text="SourceDir undefined - online install from GitHub" Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="DownloadFromGitHub"/>
</IfNotDef>
</Namespace>

<If Condition='##class(Config.Namespaces).Exists("MDX2JSON")'>
<Log Text="Setup DSW settings." Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="SetDSWSettings"/>
</If>


<Log Text="Moving to configuring monitor" Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="ConfiguringMonitor"/>


<!--<Log Text="Configuring startup namespace" Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="ConfiguringClasses"/>

<Log Text="Activating Monitor Classes" Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="ActivatingingClasses"/>

<Log Text="Starting Monitor" Level="0"/>
<RunInstall Class="kutac.monitor.utils.Installer" Method="RunningMonitor"/>-->



</Manifest>
]]></Data>
</XData>

<Method name="setup">
<Description>
 This is a method generator whose code is generated by XGL.
Set pVars("Namespace")="SYSMON"
Set pVars("SourceDir")="C:\temp\SYSMON\cls\kutac\"
do ##class(kutac.monitor.utils.Installer).setup(.pVars)</Description>
<Internal>1</Internal>
<ClassMethod>1</ClassMethod>
<CodeMode>objectgenerator</CodeMode>
<FormalSpec><![CDATA[&pVars,pLogLevel:%Integer=0,pInstaller:%Installer.Installer]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[ 	Quit ##class(%Installer.Manifest).%Generate(%compiledclass, %code, "Install")
]]></Implementation>
</Method>

<Method name="EnableDeepSee">
<ClassMethod>1</ClassMethod>
<FormalSpec>pVars,pLogLevel,tInstaller</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set namespace = tInstaller.Evaluate("${Namespace}")
	w "Turn on DeepSee flag on "_namespace_" Web Application.", !
	set tSC = $$$OK
    try {
        new $Namespace
        set $Namespace = "%SYS"
        
        set tApplication = ##class(Security.Applications).%OpenId("/csp/"_$ZCONVERT(namespace, "L"),,.tSC)
        if $$$ISERR(tSC) {
	        do $system.Status.DisplayError(tSC)
            quit
        }
        
        set tApplication.DeepSeeEnabled = 1
        set tSC = tApplication.%Save()
    } catch e {
        set tSC = e.AsStatus()
    }
    
    do $system.Status.DisplayError(tSC)
    quit tSC
]]></Implementation>
</Method>

<Method name="DownloadFromGitHub">
<ClassMethod>1</ClassMethod>
<FormalSpec>pVars,pLogLevel,tInstaller</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    Set Namespace=tInstaller.Evaluate("${Namespace}")
    Do tInstaller.PushNS("%SYS")
    Set tSC = ..Update(Namespace, "intersystems-ru", "deepsee-sysmon-dashboards", "master")
    Do tInstaller.PopNS()
    If $$$ISERR(tSC) Throw ##class(%Installer.Exception).CreateFromStatus(tSC)
    quit $$$OK
]]></Implementation>
</Method>

<Method name="Update">
<ClassMethod>1</ClassMethod>
<FormalSpec>Namespace=$Namespace,Owner:%String="intersystems-ru",Repository:%String="deepsee-sysmon-dashboards",Branch:%String,Username:%String,Password:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set namespace = $Namespace
 	Set SSLConfig = "GitHub"
 	Zn "%SYS"
 	Do:'##class(Security.SSLConfigs).Exists(SSLConfig) ##class(Security.SSLConfigs).Create(SSLConfig)

 	Set req=##class(%Net.HttpRequest).%New()
 	Set req.Https=1
	Set req.SSLConfiguration=SSLConfig
	Set req.Server="api.github.com"
	Set req.Location = "repos/" _ Owner _ "/" _ Repository _ "/contents" 	// as described in https://developer.github.com/v3/repos/	
	Do:$d(Branch) req.SetParam("ref",Branch) 								// if omitted the repository’s default branch (usually master) would be used
	Do req.SetHeader("Accept","application/vnd.github.v3+json") 			// we want to receive API v3
	
	If ($d(Username) && $d(Password)) {										// supply Username and Passwor, if both are provided. GitHub accept Basic Auth
		Set req.Username = Username											// https://developer.github.com/v3/auth/
	 	Set req.Password = Password
	} 	

 	Set links = ##class(%ListOfDataTypes).%New()
 	Set st = ..ProcessDirectory("",req,.links)
 	Return:$$$ISERR(st) st
 	 	
 	Zn Namespace
	Set st = ..DownloadFiles(links,req,.list)
	Set st2 = $system.OBJ.CompileAll()
	Zn namespace
	
 	Return $$$ADDSC(st, st2)
]]></Implementation>
</Method>

<Method name="ProcessDirectory">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[Path:%String="",Request:%Net.HttpRequest,&Links:%ListOfDataTypes]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set location = Request.Location
	Set Request.Location = Request.Location _ Path
	
	Set st = Request.Get(,,$$$NO)
	Return:$$$ISERR(st) st
	Return:(Request.HttpResponse.StatusCode = 404) $$$ERROR($$$GeneralError,"Repository doesn't exist OR you don't have access")
	Return:((Request.HttpResponse.StatusCode = 403) && (Request.HttpResponse.GetHeader("X-RATELIMIT-REMAINING")=0)) $$$ERROR($$$GeneralError,"API rate limit exceeded. Try logging in.")
 	Return:(Request.HttpResponse.StatusCode '= 200) $$$ERROR($$$GeneralError,"Received " _ Request.HttpResponse.StatusCode _ " status, expected 200")
 	
 	#dim objects As List of %ZEN.proxyObject
 	#dim obj As %ZEN.proxyObject
	Set st = ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(Request.HttpResponse.Data,,.objects,1)
	Return:$$$ISERR(st) st
	
	For i = 1:1:objects.Count() {		
		Set obj = objects.GetAt(i)
		If (obj.type = "dir") {
			Set st = ..ProcessDirectory("/"_obj.name,Request,.Links)
			Return:$$$ISERR(st) st		
		} ElseIf (obj.type = "file") {
			Do:..IsCacheFile(obj) Links.Insert(obj."download_url")
		} Else {
			// obj.type = "symlink" or obj.type = "submodule"
		} 
	}
	Set Request.Location = location // to keep track of where in the repository tree we are
	Return $$$OK
]]></Implementation>
</Method>

<Method name="IsCacheFile">
<ClassMethod>1</ClassMethod>
<FormalSpec>File:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	Set extensions = ",xml,cls,csp,csr,mac,int,bas,inc,gbl,prj,obj,pkg,gof,js,html,css"
	Return:($L(File.name,".")=1) 0 //no extension
	Set File.Extension = $P(File.name,".",$L(File.name,"."))
	Return $F(extensions,","_$ZCVT(File.Extension,"l")_",")
]]></Implementation>
</Method>

<Method name="DownloadFiles">
<ClassMethod>1</ClassMethod>
<FormalSpec>Links:%ListOfDataTypes,Request:%Net.HttpRequest,*Items</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Kill Items
	Set Request.Server = "raw.githubusercontent.com"
	Set st = $$$OK
	
	For i = 1:1:Links.Count() {
		Set link = Links.GetAt(i)
		Set streq = Request.Get($e(link,35,*)) // Remove "https://raw.githubusercontent.com/" from URL.	
		Set:$$$ISERR(streq) st=$$$ADDSC(st, streq)
		
		Set binarystream = Request.HttpResponse.Data
		Do binarystream.Rewind() // just in case
		
		Set stream=##class(%GlobalCharacterStream).%New() //translating binary stream into character stream
		While 'binarystream.AtEnd { 
			Do stream.WriteLine(binarystream.ReadLine()) 
		}
		Do stream.Rewind()
		
		
		Set strload = ""
		If ..IsUDLFormat(stream, link) {
			Set stload = ..LoadUDLFile(stream, link)
		} 
		ElseIf ..IsWebFormat(stream, link)
		{
			Set stload = ..LoadWebFile(binarystream, link,"csp/")
		}
		Else {
			Set stload = $system.OBJ.LoadStream(stream,"",.error,.items,,,,"UTF8")
		}
		
 		Set:$$$ISERR(stload) st=$$$ADDSC(st, stload)
 		Merge Items = items  // Does not overwrite existing array keys: Items(itemname)=""
	}
	
	Set Request.Server="api.github.com"
	Return st
]]></Implementation>
</Method>

<Method name="IsWebFormat">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%GlobalCharacterStream,url:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	set params = $find(url, "?")
 	if params {
 		set url = $extract(url, 1, params - 2)
 	}
	
	Set extensions = "csp,js,html,css"
	If $Find(extensions, $Piece(url, ".", *)) {
		Set st = ##class(%XML.TextReader).ParseStream(contentStream)
		Do contentStream.Rewind()
		If $$$ISERR(st) {
			Return $$$YES
		}
		Else {
			Return $$$NO
		}
	}
	Else {
		Return $$$NO
	}
]]></Implementation>
</Method>

<UDLText name="T">
<Content><![CDATA[
// Класс не универсальный, загрузку web составляющей нужно прописывать самому. Эта информация для тех кто хочет просто скопипастить этот метод.

]]></Content>
</UDLText>

<Method name="LoadWebFile">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%CacheString,url:%String,CSPDirInRepo:%String="csp/"</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set st = $$$OK
	
	Set filestream = ##class(%Stream.FileCharacter).%New()
	Do contentStream.Rewind()
	do filestream.Rewind()
	
	set CSPPath = $system.CSP.GetFileName($system.CSP.GetDefaultApp($namespace)_"/")
	set fileDirectory = $Piece(url, CSPDirInRepo, *)
	set dirChain = $p(CSPPath_fileDirectory,$p($translate(CSPPath_fileDirectory,"\","/"),"/",*),1)
	do ##class(%File).CreateDirectoryChain(dirChain)
	set st = filestream.LinkToFile(CSPPath_fileDirectory)
	
	While 'contentStream.AtEnd
	{
		Do filestream.WriteLine(contentStream.ReadLine())
	}
	if $$$ISERR(st) Quit st
	
	set st = filestream.%Save()
	Return st
]]></Implementation>
</Method>

<Method name="IsUDLFormat">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%GlobalCharacterStream,url:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	set params = $find(url, "?")
 	if params {
 		set url = $extract(url, 1, params - 2)
 	}
	
	Set extensions = "cls,mac,int,inc,dfi"
	If $Find(extensions, $Piece(url, ".", *)) {
		Set st = ##class(%XML.TextReader).ParseStream(contentStream)
		Do contentStream.Rewind()
		If $$$ISERR(st) {
			Return $$$YES
		}
		Else {
			Return $$$NO
		}
	}
	Else {
		Return $$$NO
	}
]]></Implementation>
</Method>

<Method name="LoadUDLFile">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%GlobalCharacterStream,url:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set st = $$$OK
	Set ext = ..GetExt(url)
	If ext = "cls" {
		Set st = ..CreateClass(contentStream, url)
	}
	Elseif ext = "dfi" {
		Set st = ..CreateDFI(contentStream, url)
	}
	Else {
		Set st = ..CreateOther(contentStream, url)
	}
	
	Return st
]]></Implementation>
</Method>

<Method name="GetExt">
<ClassMethod>1</ClassMethod>
<FormalSpec>url:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[	Return $ZConvert($Piece(url, ".", *), "l")
]]></Implementation>
</Method>

<Method name="CreateClass">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%CharacterStream,url:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set st = ..GetClassName(url, .className)
	Do contentStream.Rewind()
	Return:$$$ISERR(st) st
	
	If '..IsClassExist(className) {
		Set clsDef = ##class(%Dictionary.ClassDefinition).%New()
		Set clsDef.Name = className
		Set st = clsDef.%Save()
		Return:$$$ISERR(st) st
	}

	
	Set namespace = $namespace
	
	Set st = ##class(%Compiler.UDL.TextServices).SetTextFromStream(namespace, className, contentStream)
	Return st
]]></Implementation>
</Method>

<Method name="GetClassName">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[url:%String,&name]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set formattedUrl = $Translate($Translate(url, "\", "/"), "/", ".")
	Set name = $Replace($Extract(formattedUrl, $Find(formattedUrl, "cls."), *), ".cls", "")
	Return $$$OK
]]></Implementation>
</Method>

<Method name="IsClassExist">
<ClassMethod>1</ClassMethod>
<FormalSpec>className:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	Set query = "SELECT TOP 1 COUNT(ID) FROM %Dictionary.ClassDefinition WHERE ID = ?"
	Set statement = ##class(%SQL.Statement).%New()
	Set st = statement.%Prepare(query)
	Set rset = statement.%Execute(className)
	If rset.%Next() && (rset.%ROWCOUNT > 0) {
		Return 1
	}
	Return 0
]]></Implementation>
</Method>

<Method name="CreateDFI">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%CharacterStream,url:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set st = $$$OK
    Try {
	    Set st = ..GetDFIName(url, .name)
	    Return:$$$ISERR(st) st
	    
        Set tDoc = ##class(%DeepSee.UI.FolderItemDocument).%New(name)
        Set st = tDoc.ImportFromXML(contentStream)
        Return:$$$ISERR(st) st
        
        Set st = tDoc.Save()
        Return:$$$ISERR(st) st
    } Catch e {
        Set st = e.AsStatus()
    }
    Return st
]]></Implementation>
</Method>

<Method name="GetDFIName">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[url:%String,&name]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set formattedUrl = $Translate($Translate(url, "\", "/"), "/", ".")
	Set name = $Replace($Extract(formattedUrl, $Find(formattedUrl, "dfi."), *), "%20", " ")
	Return $$$OK
]]></Implementation>
</Method>

<Method name="CreateOther">
<ClassMethod>1</ClassMethod>
<FormalSpec>contentStream:%GlobalCharacterStream,url:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	Set internalName = $Piece(url, "/", *)
		
	Set rtn = ##class(%Routine).%New(internalName)
	Set status = rtn.CopyFrom(contentStream)
	Return:$$$ISERR(status) status
	
	Set status = rtn.Save()
 	Return:$$$ISERR(status) status
 	
 	Set status = rtn.Compile()
 	Return status
]]></Implementation>
</Method>

<Method name="ConfiguringMonitor">
<ClassMethod>1</ClassMethod>
<FormalSpec>pVars,pLogLevel,tInstaller</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set Namespace=tInstaller.Evaluate("${Namespace}")
	do tInstaller.PushNS("%SYS")
	
	do ..WriteLog("Activating Monitor Classes")
	set st2 = ..ActivatingClasses()
	
	do ..WriteLog("Starting Monitor")
	set st3 = ..RunningMonitor()
	
	&sql(select id into :TaskID from %SYS.Task where Name = 'Rebuild SYSMON Cubes')
	do tInstaller.PopNS()
	
	do tInstaller.PushNS(Namespace)
	if +SQLCODE {
		do ..WriteLog("Creating Task")
		Do ##class(kutac.monitor.task.Builder).Setup()
	} else {
		do ##class(%SYS.Task).Resume(TaskID)
		do ##class(%SYS.Task).RunNow(TaskID)
	}
	
	do tInstaller.PopNS()
	
	do tInstaller.PushNS("%SYS")
	set SSLConfig = "EmailDefault"
	do:'##class(Security.SSLConfigs).Exists(SSLConfig) ##class(Security.SSLConfigs).Create(SSLConfig)
	do ##class(%Monitor.Manager).AppSmtpServer(,,.SSLConfig)
	do tInstaller.PopNS()
		
	set st = $$$ADDSC(st2, st3)
	if $$$ISERR(st) Throw ##class(%Installer.Exception).CreateFromStatus(st)
    quit $$$OK
]]></Implementation>
</Method>

<Method name="ActivatingClasses">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set act = $LISTBUILD("%Monitor.System.HistoryPerf","%Monitor.System.Diskspace","%Monitor.System.License","%Monitor.System.Processes","%Monitor.System.HistorySys","%Monitor.System.Freespace","%Monitor.System.LockTable","%Monitor.System.Routines")
	set str = $LISTTOSTRING(act)
	for i = 1:1:$LISTLENGTH(act)
	{
		set cls = $PIECE(str, ",", i)
		set id = ""
		do ##class(%Monitor.ItemGroup).MetricsClassItemGroup(cls,.id)
 		s class = ##class(%Monitor.ItemGroup).%OpenId(id)
 		if '$IsObject(class)
 		{ 
 			do ..WriteLog("ERROR: Class '"_cls_"' does not exist")
 		}
 		else
 		{
	 		set class.Activated = 1
	 		set status = class.%Save()
	 		if ('status) 
	 		{
		 		w $System.Status.GetErrorText(st)
	 		}
 		}
	}
	return $$$OK
]]></Implementation>
</Method>

<Method name="RunningMonitor">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s sc = ##class(%SYS.Monitor).Stop()
	s sc1 = ##class(%SYS.Monitor).Start()
	do ..WriteLog("System Monitor has been restarted!")
	return sc1
]]></Implementation>
</Method>

<Method name="OfflineInstall">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&pVars,pLogLevel:%Integer=0,tInstaller:%Installer.Installer]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set ns = $Namespace
	set namespace = tInstaller.Evaluate("${Namespace}")
	
	zn namespace
	do $system.OBJ.ImportDir(tInstaller.Evaluate("${SourceDir}"),"*.xml","ck",.st,1)
	
	zn ns
	quit $$$OK
]]></Implementation>
</Method>

<Method name="SetDSWSettings">
<ClassMethod>1</ClassMethod>
<FormalSpec>pVars,pLogLevel,tInstaller</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set id = $classname()_"||DSWSettings"
	quit:(##class(%Dictionary.CompiledXData).%ExistsId(id) = 0) $$$ERROR($$$GeneralError,"You don't export DSW settings")	
	set xdata = ##class(%Dictionary.CompiledXData).%OpenId(id)
	
	set settings = xdata.Data.Read()
	if (##class(%Dictionary.CompiledMethod).%ExistsId("MDX2JSON.Users||SetConfig"))
	{
		do ##class(MDX2JSON.Users).SetConfig(tInstaller.Evaluate("${Namespace}"),settings,"DeepSeeWeb")
	}
	else{w "Please, update MDX2JSON."}
	quit $$$OK
]]></Implementation>
</Method>

<Method name="WriteLog">
<ClassMethod>1</ClassMethod>
<FormalSpec>text:%String</FormalSpec>
<Implementation><![CDATA[	w !, text, !
]]></Implementation>
</Method>

<XData name="DSWSettings">
<MimeType>text/plain</MimeType>
<Data><![CDATA[{"Default":{"addons":"","app":{"language":"en","hideFolders":false,"showImages":true,"isMetro":false},"namespaces":{"widgets":{"mobile/Current Performance Indicators.dashboard":{"ObjPerf":{"sizeY":7,"sizeX":3,"col":6},"GlobPerf":{"col":4,"sizeY":5,"sizeX":2}},"mobile/GloPerf.dashboard":{"w2":{"sizeY":5,"sizeX":10}},"dash/actions/State Info.dashboard":{"Widget1":{"col":0}},"mobile/ObjPerf.dashboard":{"_filters":[{"targetProperty":"[Measurement Date].[H1].[Month]","value":"&[NOW]"},{"targetProperty":"[Measurement Date].[H1].[Day]","value":"&[13]"}]},"mobile/DBSizes.dashboard":{"w2":{"series":{}}}},"tiles":{"mobile/":{"":{"row":0,"col":0,"sizeX":1,"sizeY":1,"color":2,"fontColor":0,"icon":27,"title":""},"CPU Utilization":{"row":0,"col":1,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"CPU Utilization"},"Current Performance Indicators":{"row":3,"col":2,"sizeX":2,"sizeY":2,"color":1,"fontColor":0,"icon":0,"hideTitle":true,"widget":0,"title":"Current Performance Indicators"},"Current Performance Objects":{"row":0,"col":5,"sizeX":3,"sizeY":3,"color":1,"fontColor":0,"icon":0,"widget":1,"title":"Current Performance Objects","hideTitle":true},"Database Sizes":{"row":0,"col":2,"sizeX":3,"sizeY":3,"color":1,"fontColor":0,"icon":0,"widget":1,"title":"Database Sizes"},"Globals Performance":{"row":4,"col":1,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"Globals Performance"},"Journal size history":{"row":3,"col":1,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"Journal size history"},"License History":{"row":2,"col":1,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"License History"},"Object Performance":{"row":1,"col":1,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"Object Performance"},"Routines Performance History":{"row":0,"col":8,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"Routines Performance History"},"Various System Parameters Performance":{"row":3,"col":4,"sizeX":4,"sizeY":2,"color":1,"fontColor":0,"icon":0,"widget":0,"title":"Various System Parameters Performance","hideTitle":true},"WIJ Performance":{"row":0,"col":9,"sizeX":1,"sizeY":1,"color":1,"fontColor":0,"icon":0,"title":"WIJ Performance"},"Current CPU Parameters":{"row":1,"col":8,"sizeX":2,"sizeY":2,"color":1,"fontColor":0,"icon":0,"hideTitle":true,"title":"Current CPU Parameters","widget":0},"Current License Count":{"row":3,"col":8,"sizeX":2,"sizeY":2,"color":1,"fontColor":0,"icon":1,"title":"Current License Count","hideTitle":true,"widget":0}}}}}}
]]></Data>
</XData>
</Class>
</Export>
